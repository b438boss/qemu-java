// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name
group = "org.anarres.${githubProjectName}"

buildscript {
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/github.gradle')
// apply from: file('gradle/maven.gradle')
apply from: file('gradle/nexus.gradle')
apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')

subprojects {
	group = "org.anarres.qemu"

	dependencies {
		compile "org.slf4j:slf4j-api:1.7.7"
		compile "com.google.guava:guava:18.0"
		compile 'com.fasterxml.jackson.core:jackson-databind:2.4.1.3'
	}
}

project(':qemu-qapi') {

	sourceSets {
		generate
	}

	configurations {
		generateCompile.extendsFrom compile
		generateRuntime.extendsFrom runtime
	}

	dependencies {
		generateCompile "com.google.guava:guava:18.0"
		generateCompile "org.apache.velocity:velocity:1.7"
		generateCompile "org.apache.velocity:velocity-tools:2.0"
		generateCompile "net.sf.jopt-simple:jopt-simple:4.6"
		// generateCompile "commons-logging:commons-logging:1.1.3"
		generateCompile "com.google.code.gson:gson:2.2.4"

		generateRuntime "org.slf4j:slf4j-simple:1.7.7"

		testCompile project(':qemu-manager')
		testCompile project(':qemu-exec').sourceSets.test.output
	}

	task('generate', type: JavaExec, dependsOn: compileGenerateJava) {
		outputs.upToDateWhen { false }
		doFirst {
			delete fileTree("src/main/java/org/anarres/qemu/qapi/api") {
				include "*.java"
				exclude "package-info.java"
			}
		}
		main = 'org.anarres.qemu.qapi.generator.Main'
		classpath = configurations.generateRuntime + sourceSets.generate.output
		// See also http://repo.or.cz/w/qemu/qmp-unstable.git/blob_plain/HEAD:/qapi-schema.json
	}

}

project(':qemu-image') {
	dependencies {
	}
}

project(':qemu-exec') {
	dependencies {
		compile project(':qemu-image')
		compile 'org.codehaus.mojo:animal-sniffer-annotations:1.14'

		// Can be changed to qemu-qapi when we move QEmuProcess to qemu-exec.
		testCompile project(':qemu-manager')
		// testCompile 'commons-io:commons-io:2.4'
	}
}

project(':qemu-manager') {
	dependencies {
		compile project(':qemu-qapi')
		compile project(':qemu-exec')

		testCompile project(':qemu-exec').sourceSets.test.output
	}
}

aggregateJavadoc {
	options.links "http://docs.oracle.com/javase/6/docs/api/",
		"https://google-gson.googlecode.com/svn/trunk/gson/docs/javadocs/"
	options.use true
	options.linkSource true
}
